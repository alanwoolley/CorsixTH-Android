buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

project.ext.version = "0.9.3.0"

apply plugin: 'com.android.application'
apply from: 'versioning.gradle'

repositories {
    jcenter()

    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}
dependencies {
    compile 'com.android.support:cardview-v7:27.0.2'
    compile 'pub.devrel:easypermissions:0.4.2'
    compile 'com.android.support:recyclerview-v7:27.0.2'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'commons-io:commons-io:2.4'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'
    compile 'com.android.support:multidex:1.0.2'
    compile fileTree(include: '*.jar', dir: 'jars')
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27

        multiDexEnabled true

        versionName project.ext.version
        versionCode buildVersionCode()

        ndk {
            abiFilters "armeabi-v7a"
        }

    }
    Properties signingProperties = new Properties()

    if (file("signing.properties").exists()) {
        signingProperties.load(new FileInputStream(file("signing.properties")))
        signingConfigs {
            release {
                storeFile file(signingProperties['signing.release.storeFile'])
                storePassword signingProperties['signing.release.storePassword']
                keyAlias signingProperties['signing.release.keyAlias']
                keyPassword signingProperties['signing.release.keyPassword']
            }
        }
    }

    signingConfigs {
        release {

        }
    }


    buildTypes {
        release {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
            signingConfig signingConfigs.release
        }

        beta {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
            versionNameSuffix "-BETA"
        }

        alpha {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
            versionNameSuffix "-ALPHA-" + System.getenv("BUILD_NUMBER") ?: "9999"
        }

        debug {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "true"
            ext.enableCrashlytics = false
        }


        jnidebug.initWith(buildTypes.debug)
        jnidebug {
            jniDebuggable true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/Java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.

        beta.setRoot('build-types/beta')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
    }
    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }

}

// Create ZIP of CorsixTH Lua files
task createGameZip(type: Zip) {

    destinationDir = file('assets')
    archiveName = 'game.zip'

    from('jni/src/CorsixTH') {
        include 'Levels/**'
        include 'Campaigns/**'
        include 'Lua/**'
        include 'CorsixTH.lua'
        include 'Bitmap/aux_ui.*'
        include 'Bitmap/tree_ctrl.*'
        include 'Bitmap/mainmenu1080.dat'
        include 'Bitmap/mainmenu1080.pal'

    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn createGameZip
}

