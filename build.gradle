buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

project.ext.version = "0.9.0"

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: 'versioning.gradle'

repositories {
    mavenCentral();
    maven { url 'https://maven.fabric.io/public' }

}
dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.2'
    compile 'com.android.support:multidex:1.0.0'
    compile fileTree(dir: 'jars', include: '*.jar')
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }
}

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22

        multiDexEnabled true

        versionName project.ext.version
        versionCode buildVersionCode()

    }

    Properties signingProperties = new Properties()

    if (file("signing.properties").exists()) {
        signingProperties.load(new FileInputStream(file("signing.properties")))
        signingConfigs {
            release {
                storeFile file(signingProperties['signing.release.storeFile'])
                storePassword signingProperties['signing.release.storePassword']
                keyAlias signingProperties['signing.release.keyAlias']
                keyPassword signingProperties['signing.release.keyPassword']
            }
        }


        productFlavors {
            prod {
                signingConfig signingConfigs.release
            }
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }

        prod {

        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
        }

        beta {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
            versionNameSuffix "-BETA"
        }

        alpha {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "false"
            versionNameSuffix "-ALPHA"
        }

        debug {
            buildConfigField "boolean", "ALWAYS_UPGRADE", "true"
            ext.enableCrashlytics = false
        }


        jnidebug.initWith(buildTypes.debug)
        jnidebug {
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/Java']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.

        beta.setRoot('build-types/beta')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions {
    }

    lintOptions {
        abortOnError false
    }
    
}

// Create ZIP of CorsixTH Lua files
task createGameZip(type: Zip) {

    destinationDir = file('assets')
    archiveName = 'game.zip'

    from('jni/src/CorsixTH') {
        include 'Levels/**'
        include 'Lua/**'
        include 'CorsixTH.lua'
        include 'Bitmap/aux_ui.*'
        include 'Bitmap/tree_ctrl.*'
        include 'Bitmap/mainmenu1080.dat'
        include 'Bitmap/mainmenu1080.pal'
    }
}



tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn createGameZip
}

